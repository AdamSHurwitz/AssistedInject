apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 28

    applicationId 'com.example.inject.assisted'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    checkReleaseBuilds false
  }

  signingConfigs {
    assisted {
      storeFile rootProject.file('signing.keystore')
      storePassword 'assisted'
      keyAlias 'assisted'
      keyPassword 'assisted'
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.assisted
    }
    release {
      signingConfig signingConfigs.assisted

      minifyEnabled true
      proguardFiles(file('shrinker.pro'))
    }
  }

  applicationVariants.all { variant ->
    tasks.named("test${variant.name.capitalize()}UnitTest").configure {
      // Ensure the APK has been built by the time the unit tests run.
      it.dependsOn(variant.packageApplicationProvider)

      // Supply the APK path to the tests with a system property.
      def apkRelativePath = "build/outputs/apk/${variant.name}/${variant.outputs[0].outputFileName}"
      it.systemProperty('apkPath', file(apkRelativePath).absolutePath)
    }
  }
}

dependencies {
  implementation deps.dagger
  annotationProcessor deps.daggerCompiler

  implementation project(':assisted-inject-annotations-dagger2')
  annotationProcessor project(':assisted-inject-processor-dagger2')

  testImplementation deps.kotlin
  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.dexMemberList
}
